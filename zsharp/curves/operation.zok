from "point" import Point
from "curve" import CurveConst
from "../field/mod_operation" import field_minus

// TODO: implement field addition and multiplication that safely handle overflow

// helper function for point_double
// 3 * x^2 + a
def x_prime(field x, field a, field Fp) -> field:
    field x_squared = (x * x) % Fp
    field x_squared_times_3 = (3 * x_squared) % Fp

    return (x_squared_times_3 + a) % Fp


// helper function for point_double
// 2 * x1 + x2
def x_star(field x1, field x2, field Fp) -> field:
    field x1_times_2 = (2 * x1) % Fp
    return (x1_times_2 + x2) % Fp


// helper function for point_double
// 2 * y
def y_prime(field y, field Fp) -> field:
    return (2 * y) % Fp


def point_double(Point pt, CurveConst cc) -> Point:
    field x_prime = x_prime(pt.x, cc.a, cc.Fp)
    field y_prime = y_prime(pt.y, cc.Fp)

    field mu = (x_prime / y_prime) % cc.Fp
    field mu_squared = (mu * mu) % cc.Fp
    field mu_squared_minus_x = field_minus(mu_squared, pt.x, cc.Fp)
    field x3 = field_minus(mu_squared_minus_x, pt.x, cc.Fp)

    field x_star = x_star(pt.x, pt.x, cc.Fp)
    field x_star_times_x_prime = (x_star * x_prime) % cc.Fp
    field gamma = (x_star_times_x_prime /y_prime) % cc.Fp
    field mu_cube = (mu_squared * mu) % cc.Fp

    field gamma_minus_mu_cube = field_minus(gamma, mu_cube, cc.Fp)
    field y3 = field_minus(gamma_minus_mu_cube, pt.y, cc.Fp)

    return Point{x: x3, y: y3}


def point_addition(Point pt1, Point pt2, CurveConst cc) -> Point:
    field y_delta = field_minus(pt2.y, pt1.y, cc.Fp)
    field x_delta = field_minus(pt2.x, pt1.x, cc.Fp)
    field mu = (y_delta / x_delta) % cc.Fp
    field mu_squared = (mu * mu) % cc.Fp
    field mu_squared_minus_x1 = field_minus(mu_squared, pt1.x, cc.Fp)
    field x3 = field_minus(mu_squared_minus_x1, pt2.x, cc.Fp)

    field x_star = x_star(pt1.x, pt2.x, cc.Fp)
    field x_star_times_y_delta = (x_star * y_delta) % cc.Fp
    field gamma = (x_star_times_y_delta / x_delta) % cc.Fp
    field mu_cube = (mu_squared * mu) % cc.Fp
    field gamma_minus_mu_cube = field_minus(gamma, mu_cube, cc.Fp)
    field y3 = field_minus(gamma_minus_mu_cube, pt1.y, cc.Fp)

    return Point{x: x3, y: y3}
